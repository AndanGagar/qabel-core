apply plugin: 'java'
apply plugin: "kotlin"

sourceCompatibility = 1.7
version = '0.12.1'
group = 'de.qabel.box'

ext.sharedManifest = manifest {
    attributes 'Implementation-Version': version
    attributes 'Project': 'Qabel Core'
    attributes 'Component': 'Box'
    attributes 'Built-From-Revision': "git rev-parse HEAD".execute().text.trim()
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

jar {
    manifest = project.manifest {
        from sharedManifest
        attributes 'Implementation-Title': 'Qabel Core - Box Component'
    }
}

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile project(':core')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile 'junit:junit:4.+'
    testCompile 'org.hamcrest:java-hamcrest:2.0.0.0'
    testCompile 'org.meanbean:meanbean:2.+'
    testCompile 'org.mockito:mockito-core:2.0.52-beta'
    testCompile 'com.nhaarman:mockito-kotlin:0.5.0'
}

tasks.withType(Test) {
    def platform = "${System.properties['os.name'].toLowerCase()}_${System.properties['os.arch']}"
    systemProperty "java.library.path", "../core/build/binaries/curve25519SharedLibrary/$platform/"
}

sourceSets {
    main {
        resources.srcDirs += ['src/resources/config/']
    }
    test {
        resources.srcDirs += ['src/resources/config/']
    }
}

task testJar(type: Jar) {
    from(sourceSets.test.output) {
        include "**"
    }
    baseName = 'box-test'
    manifest {
        from sharedManifest
        attributes 'Implementation-Title': 'Qabel Core - Box Component - Test artifact'
    }
}

jar.manifest.writeTo("$buildDir/manifest.mf")
testJar.manifest.writeTo("$buildDir/test-manifest.mf")
jar.dependsOn(testJar)

artifacts {
    archives jar
    //archives testJar
}
